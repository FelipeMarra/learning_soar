sp {elaborate*top-state*top-state
  (state <s> ^name tanks-soar)
-->
  (<s> ^top-state <s>)
}

############## Energy Low ###################
# Energy low 
sp {elaborate*state*energy-low
    (state <s> ^name tanks-soar
               ^io.input-link.energy <= 200)
-->
    (<s> ^missiles-energy low)
}

# Missiles low
sp {elaborate*state*missiles-low
    (state <s> ^name tanks-soar
               ^io.input-link.missiles 0)
-->
    (<s> ^missiles-energy low)
}

############ Directions ###############
sp {elaborate*directions
    (state <s> ^superstate nil)
-->
    (<s> ^direction-map <dm>)
    (<dm> ^north <north>
          ^south <south>
          ^west <west>
          ^east <east>)
    (<north> ^right east ^left west ^backward south ^forward north)
    (<south> ^right west ^left east ^backward north ^forward south)
    (<west> ^right north ^left south ^backward east ^forward west)
    (<east> ^right south ^left north ^backward west ^forward east)
}