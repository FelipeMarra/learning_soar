#################### Wander Turn Operator ##########################

#### Proposal
# If the tank is blocked in the forward direction, and not 
# blocked in the right or left directions, then propose the 
# turn operator for the unblocked direction. Also create an 
# indifferent preference for the operator. This operator 
# will also turn on the radar and set the radar-power to 13.
sp {wander*propose*turn
    (state <s> ^name wander
               ^io.input-link.blocked <blocked>)

    (<blocked> ^forward yes
               ^{<direction> << left right >> } no)
-->
    (<s> ^operator <o> + =)
    (<o> ^name turn
         ^actions <actions>)
    (<actions> ^rotate.direction <direction>
               ^radar.switch on
               ^radar-power.setting 13)
}

# If the tank is blocked in the forward direction, and in 
# both the right or left directions, then propose the turn 
# operator left.
sp {wander*propose*turn*sides-blocked
    (state <s> ^name wander
               ^io.input-link.blocked <blocked>)

    (<blocked> ^forward yes
               ^right yes
               ^left yes)
-->
    (<s> ^operator <o> +)
    (<o> ^name turn
         ^actions.rotate.direction left)
}