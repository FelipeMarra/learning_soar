#################### Clock Operator ##########################
# Test for the name of the substates make the rule o-supported
sp {all*propose*remove-sound
(state <s> ^name << wander chase retreat attack >>
           ^superstate.sound.expire-time <clock>
           ^io.input-link.clock > <clock>)
-->
    (<s> ^operator <o> + = >)
    (<o> ^name remove-sound)
}

sp {all*propose*remove-sound*changed-direction
    (state <s> ^name << wander chase retreat attack >>
               ^superstate <ss>
               ^io.input-link <input>)

    (<ss> ^sound.absolute-direction <abs-sound>
          ^direction-map.<direction>.<rel-sound> <abs-sound>)

    (<input> ^sound { <> silent <> <rel-sound> }
             ^direction <direction>)
-->
    (<s> ^operator <o> + = >)
    (<o> ^name remove-sound)
}

sp {all*apply*remove-sound
    (state <s> ^operator.name remove-sound
               ^superstate <ss>)
    (<ss> ^sound <ss-sound>)
-->
    (<ss> ^sound <ss-sound> -)
}