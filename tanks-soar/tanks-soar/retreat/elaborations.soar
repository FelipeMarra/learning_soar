## Retreat Operator Elaboration
## If there is a retreat state and there is a sound coming 
# in a given direction, record that direction.
sp {elaborate*retreat*sound*direction
    (state <s> ^name retreat
               ^io.input-link.sound {<> silent <direction>})
-->
    (<s> ^direction <direction>)
}

## If there is a retreat state and there is radar contact 
# with a tank, record forward direction.
sp {elaborate*retreat*radar*front
    (state <s> ^name retreat
               ^io.input-link.radar.tank)
-->
    (<s> ^direction forward)
}

## If there is a retreat state and there is radar contact 
# with a tank that is not in the center, record that 
# direction as a direction to avoid moving.
sp {elaborate*retreat*radar*direction
    (state <s> ^name retreat
               ^io.input-link.radar.tank.position {<dir> <center>})
-->
    (<s> ^avoid-direction <dir>)
}

sp {elaborate*sidestep-directions
    (state <s> ^name retreat)
-->
    (<s> ^side-direction <side-dir>)
    (<side-dir> ^forward right left ^backward left right
                ^right forward backward ^left backward forward)
}