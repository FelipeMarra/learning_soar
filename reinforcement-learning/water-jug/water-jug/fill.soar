#water jug propose fill
#If task water jug && a jug is not full propose fill
sp {water-jug*propose*fill
    (state <s> ^name water-jug
               ^jug <j>)
    (<j> ^empty > 0)
    -->
    (<s> ^operator <o> +)
    (<o> ^name fill
        ^fill-jug <j>)
}

#water-jug apply fill
#If operator is "fill", fill the jug
#in the fill-jug attr (contents = volume)
sp {water-jug*apply*fill
    (state <s> ^name water-jug
               ^operator <o>)

    (<o> ^name fill
         ^fill-jug <j>)

    (<j> ^volume <v>
         ^contents <c>)
    -->
    (<j> ^contents <v>
         ^contents <c> -)
}

#water-jug apply operator record last-operator fill
#if task is water-jug and the fill op. is selected
#create an state augmentation "last-operator" with the
#op.'s name and a copy of it's augmentations
sp {water-jug*apply*operator*record*last-operator*fill
    (state <s> ^name water-jug
               ^operator <o>)
    (<o> ^name fill
         ^fill-jug <j>)
-->
    (<s> ^last-operator <last-op>)
    (<last-op> ^name fill
               ^fill-jug <j>)
}

#water-jug apply operator remove old last-operator fill
#if task is water jug and fill is seleceted and last-op
#does not have the same name and jugs, remove last-op
sp {water-jug*apply*operator*remove*old*last-operator*fill
    (state <s> ^name water-jug
               ^operator <o>
               ^last-operator <last-op>)

    (<o> ^fill-jug <j>)

    -(<last-op> ^name fill
                ^fill-jug <j>)
-->
    (<s> ^last-operator <last-op> -)
}

#water-jug select operator avoid inverse fill
#if task is water jug and last op was empty <j>
#avoid fill <j>
sp {water-jug*select*operator*avoid*inverse*fill
    (state <s> ^name water-jug
               ^operator <o> +
               ^last-operator <last-op>)

    (<o> ^name fill
         ^fill-jug <j>)

    (<last-op> ^name empty
               ^empty-jug <j>)
-->
    (<s> ^operator <o> <)
}

gp {rl*water-jug*fill
   (state <s> ^name water-jug 
              ^operator <o> +
              ^jug <j1> <j2>)
   (<o> ^name fill 
         ^fill-jug.volume [3 5])
   (<j1> ^volume 3 
         ^contents [0 1 2 3])
   (<j2> ^volume 5 
         ^contents [0 1 2 3 4 5])
-->
   (<s> ^operator <o> = 0)
}