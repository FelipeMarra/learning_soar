## Record

# apply*move-boat*record*last-operator*types*1
# If an operator is selected to move one type of entity, then create an
# augmentation of the state (last-operator) with the bank of the boat, the type
# of entity being moved, the number, and that there is one type being moved.
sp {apply*move-boat*record*last-operator-types1
    (state <s> ^name mac
               ^operator <o>)

    (<o> ^name move-boat
         ^bank <bank>
         ^{ << missionaries cannibals >> <type> } <n>
         ^types 1)
-->
    (<s> ^last-operator <last-op>)

    (<last-op> ^types 1
               ^bank <bank>
               ^type <type>
               ^number <n>)
}

# apply*move-boat*record*last-operator*types*2
# If an operator is selected to move two types of entity, then create an
# augmentation of the state (last-operator) with the bank of the boat and that
# there is two types being moved.
sp {apply*move-boat*record*last-operator-types2
    (state <s> ^name mac
               ^operator <o>)

    (<o> ^name move-boat
         ^bank <bank>
         ^types 2)
-->
    (<s> ^last-operator <last-op>)

    (<last-op> ^types 2
               ^bank <bank>)
}

## Remove old recordings
sp {mac*apply*move-boat*remove*old*last-operator
    (state <s> ^name mac
               ^operator <o>
               ^<last-bank>.other-bank <o-bank>
               ^last-operator <last-op>)

    (<last-op> ^bank <o-bank>)
-->
    (<s> ^last-operator <last-op> -)
}